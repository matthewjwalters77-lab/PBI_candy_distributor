table _Measures
	lineageTag: 05b3b086-7772-4fb3-81f2-56c47603158d

	measure 'Production Costs' = CALCULATE( SUM( FCT_Sales[Cost] ), _Select_Date[Date Field] = "Ship Date" )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 43d8e3cd-9ba2-4d4b-ad94-bca2b006347b

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Gross Profit' = [Revenue] - [COGS]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: c2a7a475-9c24-43ea-8db7-8c4123952538

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure Revenue = [Total Sales] + [Revenue Accrued]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 3de27341-a7be-46a9-ba27-16f9ec06cbf6

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Gross Margin' =
			
			VAR _sales = [Revenue]
			VAR _cost = [COGS]
			RETURN DIVIDE( _sales - _cost, _sales )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Financial
		lineageTag: cea162c4-be81-42cf-9129-46ae0e6e887a

	measure 'Total Orders' = CALCULATE( DISTINCTCOUNT( FCT_Sales[Order ID] ) )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 0d239b5a-0b37-4dba-815d-4ca71c83f7fb

	measure 'Customer Count' = CALCULATE( DISTINCTCOUNT( FCT_Sales[Customer ID] ) )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 858d3484-63cf-4840-a9cb-cb5a5d5af833

	measure 'Total Units' = CALCULATE( SUM( FCT_Sales[Units] ) )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 82005aed-66e1-4def-b7db-bc7586b8b21c

	measure 'Shipping Miles' =
			
			VAR _tbl =
			    ADDCOLUMNS (
			        SUMMARIZE (
			            FCT_Sales,
			            FCT_Sales[Order ID],
			            DIM_Distribution_Class[CPM]
			        ),
			        "miles",
			            CALCULATE (
			                AVERAGE ( FCT_Sales[Haversine Miles] )
			            )
			    )
			RETURN
			    SUMX (
			        _tbl,
			        [miles]
			    )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 7a9289ee-91ce-4337-b8ba-339b49c80a00

	measure 'Shipping Costs' = CALCULATE( SUM( FCT_Sales[Shipping Cost] ), _Select_Date[Date Field] = "Delivery Date" )
		displayFolder: Financial
		lineageTag: 63e66cb3-ca0b-4f2a-969f-f6cfd250a420

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Days To Deliver' =
			
			CALCULATE(
			    AVERAGEX(
			        FCT_Sales,
			        FCT_Sales[Arrival Date] - FCT_Sales[Order Date]
			    ),
			    _Select_In_Process[Scope] = "Base"
			)
		formatString: 0.0
		displayFolder: Operations
		lineageTag: dacaf1d8-3e66-41b0-ba15-3a00520c7e36

	measure COGS = [Production Costs] + [Shipping Costs]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: a1402b44-e0e8-42c5-a28a-98df0c0bfcad

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'COGS / Unit' = DIVIDE( [COGS], [Total Units] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: a7c2e4a8-7f29-4935-b5f5-f6991d3ecf3d

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Revenue / Unit' = DIVIDE( [Revenue], [Total Units] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 3e47c22b-5595-4922-a11a-3b2b718a03ed

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Miles / Unit' = DIVIDE( [Shipping Miles], [Total Units] )
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: 3e659554-17fd-4358-ae1f-c2ca9bbdaa49

	measure 'GP / Unit' = DIVIDE( [Gross Profit], [Total Units] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 83b6c00f-e2d5-4380-aa40-ff76b0cf349b

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Miles / Order' = DIVIDE( [Shipping Miles], [Total Orders] )
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: f77bf34b-096c-483a-abcd-7f8d39a39d64

	measure 'COGS / Order' = DIVIDE( [COGS], [Total Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 84c80c82-a420-4f16-be7c-b097db8fb164

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Revenue / Order' = DIVIDE( [Revenue], [Total Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: b54d94bc-6c62-4e60-9937-8703e57dad5d

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'GP / Order' = DIVIDE( [Gross Profit], [Total Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: bbc0a8be-8273-4d3a-9034-ca65ec2627cd

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Units / Order' = DIVIDE( [Total Units], [Total Orders] )
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: b0b7a7c3-5fda-4f0f-93d0-a94ffe721f15

	measure 'Days To Ship' =
			
			CALCULATE(
			    AVERAGEX(
			        FCT_Sales,
			        FCT_Sales[Actual Ship Date] - FCT_Sales[Order Date]
			    ),
			    _Select_In_Process[Scope] = "Base"
			)
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: 44f73799-a123-47b7-be97-716cfe1f39c6

	measure 'Days In-Transit' =
			
			CALCULATE(
			    AVERAGEX(
			        FCT_Sales,
			        FCT_Sales[Arrival Date] - FCT_Sales[Actual Ship Date]
			    ),
			    _Select_In_Process[Scope] = "Base"
			)
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: de702bdd-0329-4027-8db5-c6f1c5a50b6e

	measure 'Production Cost / Order' = DIVIDE( [Production Costs], [Total Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Operations
		lineageTag: 0454944d-09d1-4bf6-b57a-c69f0bedb7fb

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Shipping Cost / Order' = DIVIDE( [Shipping Costs], [Total Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Operations
		lineageTag: c5f57413-9f05-4ee5-a2fd-737bd1253fd0

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Population Density' =
			
			VAR _area =
			    SUMX (
			        DIM_Locations,
			        DIVIDE (
			            DIM_Locations[Population],
			            DIM_Locations[Density]
			        )
			    )
			RETURN
			    DIVIDE (
			        [Total Population],
			        _area
			    )
		formatString: #,0.0
		displayFolder: Product Management
		lineageTag: 53a76c25-238c-43f0-a547-ec5e74e714f2

	measure 'Total Population' = CALCULATE( SUM( DIM_Locations[Population] ) )
		formatString: #,0
		displayFolder: Product Management
		lineageTag: ea7d0e28-06f2-4e79-a9e1-8a7a0a4be9dd

	measure 'Sales Density (100k)' =
			
			CALCULATE(
			    DIVIDE( [Total Sales] * 100000, [Total Population] ),
			    FCT_Sales
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Product Management
		lineageTag: eb40cf9f-8981-4464-ae80-3ff296789842

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Sales Opportunity' =
			
			VAR _zipswithsales =
			    CALCULATETABLE ( VALUES ( FCT_Sales[Postal Code] ), ALL ( FCT_Sales ), VALUES( 'Calendar' ) )
			VAR _base_density =
			    CALCULATE (
			        [Sales Density (100k)],
			        ALLSELECTED ( DIM_Locations ),
			        DIM_Locations[Zip Code] IN _zipswithsales
			    )
			VAR _zips_measures =
			    FILTER (
			        ADDCOLUMNS (
			            VALUES ( FCT_Sales[Postal Code] ),
			            "Revenue", [Revenue],
			            "Population", [Total Population],
			            "Density", [Sales Density (100k)]
			        ),
			        [Density] < _base_density && NOT ISBLANK ( [Density] )
			    )
			VAR _result =
			    SUMX (
			        _zips_measures,
			        DIVIDE ( _base_density, [Density] ) * [Revenue] - [Revenue]
			    )
			RETURN
			    _result
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Product Management
		lineageTag: 02289b1c-32d4-4f32-98f0-b28e2089531d

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure Actuals =
			
			CALCULATE(
			    SWITCH( TRUE(),
			        HASONEVALUE( '_P&L'[P&L Item] ),
			        SWITCH( SELECTEDVALUE( '_P&L'[P&L Item] ),
			            "Sales", [Total Sales],
			            "Rev Accrual", [Revenue Accrued],
			            "Production", -[Production Costs],
			            "Shipping", -[Shipping Costs],
			            "Gross Profit", [Gross Profit],
			            "Gross Margin", [Gross Margin],
			            "SG&A", -[SG&A],
			            "Marketing", -[Marketing Costs],
			            "Depreciation", -[Depreciation],
			            "EBIT", [EBIT],
			            "EBIT %", [EBIT %]
			            ),
			        HASONEVALUE( '_P&L'[P&L Category] ),
			        SWITCH( SELECTEDVALUE( '_P&L'[P&L Category] ),
			            "Revenue", [Revenue],
			            "COGS", -[COGS],
			            "Gross Profit", [Gross Profit],
			            "OpEx", -[Operating Expenses],
			            "Operating Profit", [EBIT]),
			            [EBIT]
			        ),
			        'Calendar'
			)
		displayFolder: Financial
		lineageTag: 6ab45c85-2d57-4c60-94ba-53395c0b41ae

		formatStringDefinition = IF( SELECTEDVALUE( '_P&L'[Units] ) = "%", "0.0%", "\$#,0.00;(\$#,0.00);\$#,0.00" )

	measure 'Total Sales' = CALCULATE( SUM( FCT_Sales[Sales] ), 'Calendar' )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 463b57fe-a1f0-42b2-9a07-eee337b7fe63

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Pct of Revenue' =
			
			DIVIDE(
			    CALCULATE( ABS( [Actuals] ),
			    KEEPFILTERS( '_P&L'[P&L Item] <> "Gross Margin")
			    ), [Revenue]
			)
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Financial
		lineageTag: ca23ad81-62a1-43d2-bb6d-115e4c7f5540

	measure 'Actuals PY' = IF( HASONEVALUE( 'Calendar'[Year] ), CALCULATE( [Actuals], DATEADD( 'Calendar'[Date], -12, MONTH ) ) )
		displayFolder: Financial
		lineageTag: 0323df7a-50f7-4673-a48a-76063dd14f10

		formatStringDefinition = IF( SELECTEDVALUE( '_P&L'[Units] ) = "%", "0.0%", "\$#,0.00;(\$#,0.00);\$#,0.00" )

	measure 'Actuals vs PY' =
			
			VAR _actuals = [Actuals]
			VAR _py = [Actuals PY]
			RETURN
			IF( NOT ISBLANK( _actuals ) && NOT ISBLANK( _py ), _actuals - _py )
		displayFolder: Financial
		lineageTag: 0b476227-f8c0-42ce-965a-ec132dfa83be

		formatStringDefinition = IF( SELECTEDVALUE( '_P&L'[Units] ) = "%", "0.0%", "\$#,0.00;(\$#,0.00);\$#,0.00" )

	measure 'Actuals vs PY%' =
			
			VAR _actuals = [Actuals]
			VAR _py = [Actuals PY]
			VAR _divisor = IF( _py < 0, _py * -1, _py )
			RETURN IF( NOT ISBLANK( _actuals ) && NOT ISBLANK( _py ), DIVIDE( _actuals - _py, _divisor ) )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Financial
		lineageTag: 0518503d-f10c-4e77-93bb-aad984a9a477

	measure 'Revenue Accrued' =
			
			VAR _maxdate = MAX( 'Calendar'[Date] )
			RETURN
			CALCULATE(
			    VAR _invoiced = CALCULATE( [Total Sales], USERELATIONSHIP( FCT_Sales[Invoice Date], 'Calendar'[adjusted_date] ) )
			    VAR _delivered = CALCULATE( [Total Sales], USERELATIONSHIP( FCT_Sales[Arrival Date], 'Calendar'[adjusted_date] ) )
			    RETURN _delivered - _invoiced,
			    ALL( 'Calendar' ),
			    'Calendar'[Date] <= _maxdate,
			    'Calendar'
			)
		displayFolder: Financial
		lineageTag: ddd84757-4a7b-418c-820a-2bdee3b7a432

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure SG&A = CALCULATE( [Revenue] ) * 0.1
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 4c7980ca-55a8-4db2-b911-191e36b175fd

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Marketing Costs' = CALCULATE( [Revenue] ) * 0.025
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: d3af84f7-cb90-41cd-a782-4309e8a81631

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure EBIT = [Gross Profit] - [SG&A] - [Depreciation] - [Marketing Costs]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 1aca78e8-9c0c-407a-9e96-9a9ff084f8ff

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure Depreciation = [Total Units] * 0.15
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 6e9ea5b8-b0dc-42be-be4d-82a9cdd0d113

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	measure 'Operating Expenses' = [SG&A] + [Marketing Costs] + [Depreciation]
		displayFolder: Financial
		lineageTag: 80702399-28ec-4165-a9aa-b437bf969d18

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'EBIT %' = DIVIDE( [EBIT], [Revenue] )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Financial
		lineageTag: 459c603e-94c3-4eab-99a7-dd6092b81b7e

	measure 'Prod Mgmt Metric' =
			
			SWITCH( SELECTEDVALUE( _PM[Metric] ),
			    "Sales", [Total Sales],
			    "Sales Opportunity", [Sales Opportunity],
			    "Gross Profit", [Gross Profit],
			    [Total Sales]
			)
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Product Management
		lineageTag: cd4e3148-0dd9-42fb-bd0e-bc137f233369

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	column Value
		isHidden
		formatString: 0
		lineageTag: 50395542-860f-462c-b6c9-ee72817f61a3
		summarizeBy: sum
		isNameInferred
		sourceColumn: [Value]

		annotation SummarizationSetBy = Automatic

	partition _Measures = calculated
		mode: import
		source = {BLANK()}

	annotation PBI_Id = a0f21ad30f9943cc97e7bde8aa1326e8

