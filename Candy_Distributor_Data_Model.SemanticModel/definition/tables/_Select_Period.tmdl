/// Calculation group to select periods for time intelligence
table _Select_Period
	lineageTag: bb585d2b-7e29-4625-9612-34fee59fa76e

	calculationGroup
		precedence: 70

		calculationItem Base = SELECTEDMEASURE()

		calculationItem MTD =
				
				IF (
				    NOT ISBLANK ( SELECTEDMEASURE() ),
				    CALCULATE (
				        SELECTEDMEASURE(),
				        DATESMTD ( 'Calendar'[Date] )
				    )
				)

		calculationItem QTD =
				
				IF (
				    NOT ISBLANK ( SELECTEDMEASURE() ),
				    CALCULATE (
				        SELECTEDMEASURE(),
				        DATESQTD ( 'Calendar'[Date] )
				    )
				)

		calculationItem YTD =
				
				IF (
				    NOT ISBLANK ( SELECTEDMEASURE() ),
				    CALCULATE (
				        SELECTEDMEASURE(),
				        DATESYTD ( 'Calendar'[Date] )
				    )
				)

		calculationItem TTM =
				
				CALCULATE(
				    SELECTEDMEASURE(),
				    DATESINPERIOD( 'Calendar'[Date], MAX( 'Calendar'[Date] ), -12, MONTH )
				)

		calculationItem PY = CALCULATE( SELECTEDMEASURE(), DATEADD( 'Calendar'[Date], -1, YEAR ) )

		calculationItem 'Rolling Avg (7Day)' =
				
				IF (
				    HASONEVALUE ( 'Calendar'[Date] )
				        && NOT ISBLANK ( SELECTEDMEASURE() ),
				    AVERAGEX (
				        DATESINPERIOD (
				            'Calendar'[Date],
				            MAX ( 'Calendar'[Date] ),
				            -7,
				            DAY
				        ),
				        SELECTEDMEASURE()
				    )
				)

		calculationItem 'Rolling Avg (28Day)' =
				
				IF (
				    HASONEVALUE ( 'Calendar'[Date] )
				        && NOT ISBLANK ( SELECTEDMEASURE() ),
				    AVERAGEX (
				        DATESINPERIOD (
				            'Calendar'[Date],
				            MAX ( 'Calendar'[Date] ),
				            -28,
				            DAY
				        ),
				        SELECTEDMEASURE()
				    )
				)

		calculationItem 'Rolling Avg (3Mo)' =
				
				IF (
				    HASONEVALUE ( 'Calendar'[End of Month] )
				        && COUNTROWS( 'Calendar' ) > 27
				        && NOT ISBLANK ( SELECTEDMEASURE() ),
				    CALCULATE (
				        AVERAGEX (
				            VALUES ( 'Calendar'[End of Month] ),
				            SELECTEDMEASURE()
				        ),
				        DATESINPERIOD (
				            'Calendar'[Date],
				            MAX ( 'Calendar'[Date] ),
				            -3,
				            MONTH
				        )
				    )
				)

		calculationItem 'Rolling Avg (12Mo)' =
				
				IF (
				    HASONEVALUE ( 'Calendar'[End of Month] )
				        && COUNTROWS( 'Calendar' ) > 27
				        && NOT ISBLANK ( SELECTEDMEASURE() ),
				    CALCULATE (
				        AVERAGEX (
				            VALUES ( 'Calendar'[End of Month] ),
				            SELECTEDMEASURE()
				        ),
				        DATESINPERIOD (
				            'Calendar'[Date],
				            MAX ( 'Calendar'[Date] ),
				            -12,
				            MONTH
				        )
				    )
				)

	column Period
		dataType: string
		lineageTag: 67346145-bb8e-4de8-9d87-34043ec9fca8
		summarizeBy: none
		sourceColumn: Name
		sortByColumn: Ordinal

		annotation SummarizationSetBy = Automatic

	column Ordinal
		dataType: int64
		formatString: 0
		lineageTag: a7899a1d-95bf-45a0-a70d-5263364e6d7e
		summarizeBy: sum
		sourceColumn: Ordinal

		annotation SummarizationSetBy = Automatic

