table _Measures
	lineageTag: 05b3b086-7772-4fb3-81f2-56c47603158d

	/// Expenses associated with production.
	measure 'Production Costs' =
			
			CALCULATE ( SUM ( FCT_Sales[Cost] ) )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 43d8e3cd-9ba2-4d4b-ad94-bca2b006347b

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Difference of Revenue and COGS.
	measure 'Gross Profit' =
			
			[Revenue] - [COGS]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: c2a7a475-9c24-43ea-8db7-8c4123952538

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Sum of sales and revenue accrued.
	measure Revenue =
			
			[Total Sales] + [Revenue Accrued]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 3de27341-a7be-46a9-ba27-16f9ec06cbf6

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Ratio of Gross Profit to Revenue.
	measure 'Gross Margin' =
			
			VAR _sales = [Revenue]
			VAR _cost = [COGS]
			RETURN
			    DIVIDE ( _sales - _cost, _sales )
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Financial
		lineageTag: cea162c4-be81-42cf-9129-46ae0e6e887a

	/// Count of distinct Order ID's from transactions.
	measure Orders =
			
			CALCULATE ( DISTINCTCOUNT ( FCT_Sales[Order ID] ) )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 0d239b5a-0b37-4dba-815d-4ca71c83f7fb

	/// Count of unique customer ID's in transactions.
	measure 'Customer Count' =
			
			CALCULATE ( DISTINCTCOUNT ( FCT_Sales[Customer ID] ) )
		formatString: #,0
		displayFolder: Product Management
		lineageTag: 858d3484-63cf-4840-a9cb-cb5a5d5af833

	/// Represents the number of units sold and delivered.
	measure Units =
			
			CALCULATE ( SUM ( FCT_Sales[Units] ) )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 82005aed-66e1-4def-b7db-bc7586b8b21c

	/// Represents the Haversine Miles distance from factory for orders.
	measure 'Shipping Miles' =
			
			VAR _tbl =
			    ADDCOLUMNS (
			        SUMMARIZE ( FCT_Sales, FCT_Sales[Order ID], DIM_Distribution_Class[CPM] ),
			        "miles", CALCULATE ( AVERAGE ( FCT_Sales[Haversine Miles] ) )
			    )
			RETURN
			    SUMX ( _tbl, [miles] )
		formatString: #,0
		displayFolder: Operations
		lineageTag: 7a9289ee-91ce-4337-b8ba-339b49c80a00

	/// Expenses associated with shipping.
	measure 'Shipping Costs' =
			
			CALCULATE ( SUM ( FCT_Sales[Shipping Cost] ) )
		displayFolder: Financial
		lineageTag: 63e66cb3-ca0b-4f2a-969f-f6cfd250a420

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Average days difference between order date and delivery date.
	measure 'Days To Deliver' =
			
			DeliverDate (
			    CALCULATE (
			        AVERAGEX (
			            FCT_Sales,
			            DATEDIFF ( FCT_Sales[Order Date], FCT_Sales[Arrival Date], DAY )
			        ),
			        KEEPFILTERS ( NOT ISBLANK ( 'Calendar'[Date] ) )
			    )
			)
		formatString: 0.00
		displayFolder: Operations
		lineageTag: dacaf1d8-3e66-41b0-ba15-3a00520c7e36

		kpi
			targetExpression =
				
				CALCULATE ( [Days To Deliver], PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR ) )
			targetFormatString: #,##0.00
			statusGraphic: Road Signs
			statusExpression =
				
				VAR _tolerance = 0.01
				VAR _value = [Days To Deliver]
				VAR _prev =
				    CALCULATE ( [Days To Deliver], PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR ) )
				VAR _ratio =
				    DIVIDE ( _value - _prev, _prev )
				RETURN
				    SWITCH ( TRUE (), _ratio < - _tolerance, 1, _ratio > _tolerance, -1, 0 )

	/// Cost of Goods Sold (COGS) is the sum of production and shipping costs.
	measure COGS =
			
			[Production Costs] + [Shipping Costs]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: a1402b44-e0e8-42c5-a28a-98df0c0bfcad

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// COGS expenses per Unit produced and delivered.
	measure 'COGS / Unit' =
			
			DIVIDE ( [COGS], [Units] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: a7c2e4a8-7f29-4935-b5f5-f6991d3ecf3d

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Revenue per unit ordered and delivered.
	measure 'Revenue / Unit' =
			
			DIVIDE ( [Revenue], [Units] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 3e47c22b-5595-4922-a11a-3b2b718a03ed

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Ratio of total shipping miles to total units.
	measure 'Miles / Unit' =
			
			DIVIDE ( [Shipping Miles], [Units] )
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: 3e659554-17fd-4358-ae1f-c2ca9bbdaa49

	/// Gross Profit earned per Unit produced and delivered.
	measure 'GP / Unit' =
			
			DIVIDE ( [Gross Profit], [Units] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 83b6c00f-e2d5-4380-aa40-ff76b0cf349b

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Ration of total shipping miles to total orders.
	measure 'Miles / Order' =
			
			DIVIDE ( [Shipping Miles], [Orders] )
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: f77bf34b-096c-483a-abcd-7f8d39a39d64

	/// COGS expense per order.
	measure 'COGS / Order' =
			
			DIVIDE ( [COGS], [Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 84c80c82-a420-4f16-be7c-b097db8fb164

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Revenue per order produced and delivered.
	measure 'Revenue / Order' =
			
			DIVIDE ( [Revenue], [Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: b54d94bc-6c62-4e60-9937-8703e57dad5d

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Gross Profit earned per Order produced and delivered.
	measure 'GP / Order' =
			
			DIVIDE ( [Gross Profit], [Orders] )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: bbc0a8be-8273-4d3a-9034-ca65ec2627cd

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Represents the average number of units per order.
	measure 'Units / Order' =
			
			DIVIDE ( [Units], [Orders] )
		formatString: #,0.0
		displayFolder: Operations
		lineageTag: b0b7a7c3-5fda-4f0f-93d0-a94ffe721f15

	/// Average days difference between order date and ship date.
	measure 'Days To Ship' =
			
			ShipDate (
			    CALCULATE (
			        AVERAGEX (
			            FCT_Sales,
			            DATEDIFF ( FCT_Sales[Order Date], FCT_Sales[Actual Ship Date], DAY )
			        ),
			        KEEPFILTERS ( NOT ISBLANK ( 'Calendar'[Date] ) )
			    )
			)
		formatString: #,0.00
		displayFolder: Operations
		lineageTag: 44f73799-a123-47b7-be97-716cfe1f39c6

		kpi
			targetExpression =
				
				CALCULATE ( [Days To Ship], PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR ) )
			targetFormatString: #,##0.00
			statusGraphic: Road Signs
			statusExpression =
				
				VAR _tolerance = 0.01
				VAR _value = [Days To Ship]
				VAR _prev =
				    CALCULATE ( [Days To Ship], PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR ) )
				VAR _ratio =
				    DIVIDE ( _value - _prev, _prev )
				RETURN
				    SWITCH ( TRUE (), _ratio < - _tolerance, 1, _ratio > _tolerance, -1, 0 )

	/// Average days difference between ship date and delivery date.
	measure 'Days In-Transit' =
			
			DeliverDate (
			    CALCULATE (
			        AVERAGEX (
			            FCT_Sales,
			            DATEDIFF ( FCT_Sales[Actual Ship Date], FCT_Sales[Arrival Date], DAY )
			        ),
			        KEEPFILTERS ( NOT ISBLANK ( 'Calendar'[Date] ) )
			    )
			)
		displayFolder: Operations
		lineageTag: de702bdd-0329-4027-8db5-c6f1c5a50b6e

		kpi
			targetExpression =
				
				CALCULATE ( [Days In-Transit], PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR ) )
			targetFormatString: #,##0.00
			statusGraphic: Road Signs
			statusExpression =
				
				VAR _tolerance = 0.01
				VAR _value = [Days In-Transit]
				VAR _prev =
				    CALCULATE ( [Days In-Transit], PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR ) )
				VAR _ratio =
				    DIVIDE ( _value - _prev, _prev )
				RETURN
				    SWITCH ( TRUE (), _ratio < - _tolerance, 1, _ratio > _tolerance, -1, 0 )

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Production expenses per Order produced and delivered.
	measure 'Production Cost / Order' =
			
			ShipDate ( DIVIDE ( [Production Costs], [Orders] ) )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Operations
		lineageTag: 0454944d-09d1-4bf6-b57a-c69f0bedb7fb

		kpi
			targetExpression =
				
				CALCULATE (
				    [Production Cost / Order],
				    PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR )
				)
			targetFormatString: $ #,##0.00
			statusGraphic: Road Signs
			statusExpression =
				
				VAR _tolerance = 0.01
				VAR _value = [Production Cost / Order]
				VAR _prev =
				    CALCULATE (
				        [Production Cost / Order],
				        PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR )
				    )
				VAR _ratio =
				    DIVIDE ( _value - _prev, _prev )
				RETURN
				    SWITCH ( TRUE (), _ratio < - _tolerance, 1, _ratio > _tolerance, -1, 0 )

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Shipping expenses per Order produced and delivered.
	measure 'Shipping Cost / Order' =
			
			DeliverDate ( DIVIDE ( [Shipping Costs], [Orders] ) )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Operations
		lineageTag: c5f57413-9f05-4ee5-a2fd-737bd1253fd0

		kpi
			targetExpression =
				
				CALCULATE (
				    [Shipping Cost / Order],
				    PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR )
				)
			targetFormatString: $ #,##0.00
			statusGraphic: Road Signs
			statusExpression =
				
				VAR _tolerance = 0.01
				VAR _value = [Shipping Cost / Order]
				VAR _prev =
				    CALCULATE (
				        [Shipping Cost / Order],
				        PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR )
				    )
				VAR _ratio =
				    DIVIDE ( _value - _prev, _prev )
				RETURN
				    SWITCH ( TRUE (), _ratio < - _tolerance, 1, _ratio > _tolerance, -1, 0 )

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Weighted average of population density.
	measure 'Population Density' =
			
			VAR _area =
			    SUMX (
			        DIM_Locations,
			        DIVIDE ( DIM_Locations[Population], DIM_Locations[Density] )
			    )
			RETURN
			    DIVIDE ( [Total Population], _area )
		formatString: #,0.0
		displayFolder: Product Management
		lineageTag: 53a76c25-238c-43f0-a547-ec5e74e714f2

	/// Sum of population for locations.
	measure 'Total Population' =
			
			CALCULATE ( SUM ( DIM_Locations[Population] ) )
		formatString: #,0
		displayFolder: Product Management
		lineageTag: ea7d0e28-06f2-4e79-a9e1-8a7a0a4be9dd

	/// Represents concentration of sales for zip codes that have sales.
	measure 'Sales Density (100k)' =
			
			CALCULATE ( DIVIDE ( [Total Sales] * 100000, [Total Population] ), FCT_Sales )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Product Management
		lineageTag: eb40cf9f-8981-4464-ae80-3ff296789842

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Represents the possible improvement in sales if zip codes with below average sales density was improved to the average.
	measure 'Sales Opportunity' =
			
			VAR _zipswithsales =
			    CALCULATETABLE (
			        VALUES ( FCT_Sales[Postal Code] ),
			        ALL ( FCT_Sales ),
			        VALUES ( 'Calendar' )
			    )
			VAR _base_density =
			    CALCULATE (
			        [Sales Density (100k)],
			        ALLSELECTED ( DIM_Locations ),
			        DIM_Locations[Zip Code] IN _zipswithsales
			    )
			VAR _zips_measures =
			    FILTER (
			        ADDCOLUMNS (
			            VALUES ( FCT_Sales[Postal Code] ),
			            "Revenue", [Revenue],
			            "Population", [Total Population],
			            "Density", [Sales Density (100k)]
			        ),
			        [Density] < _base_density && NOT ISBLANK ( [Density] )
			    )
			VAR _result =
			    SUMX (
			        _zips_measures,
			        DIVIDE ( _base_density, [Density] ) * [Revenue] - [Revenue]
			    )
			RETURN
			    _result
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Product Management
		lineageTag: 02289b1c-32d4-4f32-98f0-b28e2089531d

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Income associated with sales.
	measure 'Total Sales' =
			
			CALCULATE ( SUM ( FCT_Sales[Sales] ) )
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 463b57fe-a1f0-42b2-9a07-eee337b7fe63

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Ratio of selected metric to Revenue.
	measure 'Pct of Revenue' =
			
			DIVIDE (
			    CALCULATE ( ABS ( [KPI] ), KEEPFILTERS ( _Metrics[Units] = "USD" ) ),
			    [Revenue]
			)
		formatString: 0.0%;-0.0%;0.0%
		displayFolder: Financial
		lineageTag: ca23ad81-62a1-43d2-bb6d-115e4c7f5540

	/// Represents the revenue reversal of previous month and delivered sales not invoiced for the current month.
	measure 'Revenue Accrued' =
			
			VAR _maxdate =
			    MAX ( 'Calendar'[Date] )
			VAR _prevmonth =
			    EOMONTH ( _maxdate, -1 )
			VAR _currentvalue =
			    DeliverDate ( [Total Sales] ) - InvoiceDate ( [Total Sales] )
			VAR _prevvalue =
			    CALCULATE (
			        DeliverDate ( [Total Sales] ) - InvoiceDate ( [Total Sales] ),
			        DATEADD ( 'Calendar'[Date], -1, MONTH )
			    )
			RETURN
			    _currentvalue - _prevvalue
		displayFolder: Financial
		lineageTag: ddd84757-4a7b-418c-820a-2bdee3b7a432

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Expenses associated with Sales, General, and Administration.
	measure SG&A =
			
			CALCULATE ( [Revenue] ) * 0.1
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 4c7980ca-55a8-4db2-b911-191e36b175fd

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Expenses associated with marketing.
	measure 'Marketing Costs' =
			
			CALCULATE ( [Revenue] ) * 0.025
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: d3af84f7-cb90-41cd-a782-4309e8a81631

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Earnings Before Interest and Taxes (EBIT) equals Gross Profit  minus the sum of SG&A, Depreciation, and Marketing expenses.
	measure EBIT =
			
			[Gross Profit] - [SG&A] - [Depreciation] - [Marketing Costs]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 1aca78e8-9c0c-407a-9e96-9a9ff084f8ff

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Represents depreciation expense and is calculated at $0.15 USD for each unit produced and delivered.
	measure Depreciation =
			
			[Units] * 0.15
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Financial
		lineageTag: 6e9ea5b8-b0dc-42be-be4d-82a9cdd0d113

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	/// Sum of expenses for SG&A, Marketing, and Depreciation.
	measure 'Operating Expenses' =
			
			[SG&A] + [Marketing Costs] + [Depreciation]
		displayFolder: Financial
		lineageTag: 80702399-28ec-4165-a9aa-b437bf969d18

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	/// Ratio of EBIT to Revenue.
	measure 'EBIT %' =
			
			DIVIDE ( [EBIT], [Revenue] )
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Financial
		lineageTag: 459c603e-94c3-4eab-99a7-dd6092b81b7e

	/// COGS expense per order for operations, which uses the ship date for production and delivery date for shipping expense.
	measure 'COGS / Order (Ops)' =
			
			[Production Cost / Order] + [Shipping Cost / Order]
		formatString: \$#,0.00;(\$#,0.00);\$#,0.00
		displayFolder: Operations
		lineageTag: 7d999efc-4cee-45e8-bf1f-bbdbea69fb17

		kpi
			targetExpression =
				
				CALCULATE (
				    [COGS / Order (Ops)],
				    PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR )
				)
			targetFormatString: $ #,##0.00
			statusGraphic: Road Signs
			statusExpression =
				
				VAR _tolerance = 0.01
				VAR _value = [COGS / Order (Ops)]
				VAR _prev =
				    CALCULATE (
				        [COGS / Order (Ops)],
				        PARALLELPERIOD ( 'Calendar'[Date], -1, YEAR )
				    )
				VAR _ratio =
				    DIVIDE ( _value - _prev, _prev )
				RETURN
				    SWITCH ( TRUE (), _ratio < - _tolerance, 1, _ratio > _tolerance, -1, 0 )

		annotation PBI_FormatHint = {"currencyCulture":"en-US"}

	column Value
		isHidden
		formatString: 0
		lineageTag: 50395542-860f-462c-b6c9-ee72817f61a3
		summarizeBy: sum
		isNameInferred
		sourceColumn: [Value]

		annotation SummarizationSetBy = Automatic

	partition _Measures = calculated
		mode: import
		source = {BLANK()}

	annotation PBI_Id = a0f21ad30f9943cc97e7bde8aa1326e8

